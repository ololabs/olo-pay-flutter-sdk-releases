group 'com.olo.flutter.olo_pay_sdk'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.8.22'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.6.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.olo.flutter.olo_pay_sdk'
    }

    compileSdk 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    testOptions {
        managedDevices {
            localDevices {
                pixel2api27 {
                    device = "Pixel 2"
                    apiLevel = 27
                    systemImageSource = "aosp"
                }
                pixel2api30 {
                    device = "Pixel 2"
                    apiLevel = 30
                    systemImageSource = "aosp-atd"
                }
                pixel2api34 {
                    device = "Pixel 2"
                    apiLevel = 34
                    systemImageSource = "aosp-atd"
                }
            }
            groups {
                allDevices {
                    targetDevices.add(devices.pixel2api27)
                    targetDevices.add(devices.pixel2api30)
                    targetDevices.add(devices.pixel2api34)
                }
            }
        }
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    defaultConfig {
        minSdkVersion 23
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    dependencies {
        implementation "com.olo.olopay:olo-pay-android-sdk:4.1.1"
        implementation "androidx.constraintlayout:constraintlayout:2.2.1"
        implementation "androidx.core:core-ktx:1.15.0"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0"
        implementation 'com.google.android.material:material:1.12.0'
        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testImplementation 'org.mockito:mockito-core:5.10.0'
        testImplementation "org.mockito.kotlin:mockito-kotlin:5.0.0"
        testImplementation "junit:junit:4.13.2"
        androidTestImplementation "androidx.test.ext:junit:1.2.1"
        androidTestImplementation "androidx.test.ext:junit-ktx:1.2.1"
        androidTestImplementation "androidx.test.espresso:espresso-core:3.6.1"
        androidTestImplementation 'org.mockito:mockito-android:5.10.0'
        androidTestImplementation 'androidx.test:runner:1.6.2'
        androidTestUtil 'androidx.test:orchestrator:1.5.1'
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }

            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }
    }
}

dependencies {
    androidTestImplementation 'org.testng:testng:6.9.6'
}

configurations.configureEach {
    resolutionStrategy {
        force 'com.google.guava:guava:31.1-android'
    }
}